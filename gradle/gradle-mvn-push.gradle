// Adapted from https://github.com/codepath/android_guides/wiki/Building-your-own-Android-library

apply plugin: 'maven'
apply plugin: 'com.vanniktech.android.javadoc'

def isReleaseBuild() {
    return POM_VERSION_NAME.contains("SNAPSHOT") == false
}

def getJarsOutputDir() {
    if (isReleaseBuild()) {
        return "${project.buildDir}/releases"
    } else {
        return "${project.buildDir}/snapshots"
    }
}

def getJarsDestUrl() {
    if (isReleaseBuild()) {
        return "s3://${S3_BUCKET}/${S3_DIRECTORY}/maven/releases"
    } else {
        return "s3://${S3_BUCKET}/${S3_DIRECTORY}/maven/snapshots"
    }
}


def getDocsOutputDir() {
    return "${project.projectDir}/javaDoc/release"
}

def getDocsDestUrl() {
    return "s3://${S3_BUCKET}/${S3_DIRECTORY}/docs/${POM_VERSION_NAME}"
}

uploadArchives {
    repositories {
        mavenDeployer {

            pom {
                name = POM_NAME
                packaging = POM_PACKAGING
                version = POM_VERSION_NAME
                artifactId = POM_ARTIFACT_ID
                groupId = POM_GROUP
            }

            repository(url: "file:///" + getJarsOutputDir())
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    baseName = POM_ARTIFACT_ID
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    baseName = POM_ARTIFACT_ID
    from android.sourceSets.main.java.srcDirs
}

// These will be uploaded but Android Studio does not know how to use them
artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

task copyJarsToS3(type: Exec) {
    commandLine 'aws', 's3', 'cp', '--acl', 'public-read', '--recursive', getJarsOutputDir(), getJarsDestUrl()
}

copyJarsToS3.dependsOn uploadArchives

task copyDocsToS3(type: Exec) {
    commandLine 'aws', 's3', 'cp', '--acl', 'public-read', '--recursive', getDocsOutputDir(), getDocsDestUrl()
}
