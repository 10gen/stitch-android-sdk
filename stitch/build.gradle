apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

ext {
    bintrayRepo = 'MongoDB'
    bintrayName = 'org.mongodb:stitch'
    bintrayOrg = 'mongodb'

    publishedGroupId = 'org.mongodb'
    libraryName = 'Stitch Android SDK'
    artifact = 'stitch'

    libraryDescription = 'The MongoDB Stitch Android SDK'

    siteUrl = 'https://www.mongodb.com/cloud/stitch'
    gitUrl = 'https://github.com/mongodb/stitch-android-sdk.git'

    libraryVersion = '3.0.3'

    developerId = 'mongodb'
    developerName = 'MongoDB, Inc.'
    developerEmail = 'stitch@mongodb.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

if (rootProject.file('gradle/gradle-mvn-push.gradle').exists()) {
    apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
}

android {
    compileSdkVersion 27
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 20180419
        versionName "3.0.3"

        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled true
            sourceSets {
                androidTest {
                    manifest.srcFile 'src/androidTest/AndroidTestManifest.xml'
                }
            }
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
            testCoverageEnabled false
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    testOptions {
        unitTests.all {
            beforeTest { descriptor ->
                logger.lifecycle("Running test: $descriptor.className")
            }
        }
    }
    adbOptions {
        timeOutInMs 10 * 60 * 1000  // 10 minutes
        installOptions "-d","-t"
    }

    packagingOptions {
        pickFirst 'protobuf.meta'
    }
}

repositories {
    mavenCentral()
    jcenter()
    google()
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:23.1.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    // Set this dependency to use JUnit 4 rules
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    // Set this dependency to build and run Espresso tests
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    // Set this dependency to build and run UI Automator tests
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'com.github.andrzejchm.RESTMock:android:0.2.2'
    // Set this dependency to enable kotlin for android tests
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    androidTestImplementation 'io.jsonwebtoken:jjwt:0.9.0'
    androidTestImplementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"
    androidTestImplementation 'com.google.android.gms:play-services-base:11.4.2'
    androidTestImplementation "com.google.android.gms:play-services-gcm:11.4.2"

    androidTestImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    testImplementation 'junit:junit:4.12'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.0.1'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'com.google.android.gms:play-services-base:11.4.2'
    implementation "com.google.android.gms:play-services-gcm:11.4.2"

    implementation 'org.mongodb:bson:3.5.0'

    // TODO: Using fork with fixes until https://code.google.com/p/android/issues/detail?id=209471 is resolved
    implementation 'com.mcxiaoke.volley:library:1.0.19'
}


if (rootProject.file('local.properties').exists()) {
    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
    apply from: 'https://gist.githubusercontent.com/edaniels/c9e9da05491cfda34151a9275df3eb2f/raw/4acaa3de0ba08ea62d0e05cd1803e044e1bdb80f/bintrayv1.gradle'
}

